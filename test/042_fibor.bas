10 REM FIBONACCI RECURSION DEMO
20 OPTION BASE 1
30 DIM S(50),K(50)
40 LET T=0
50 LET D=0
60 REM PRINT "WHAT IS X";
70 LET X = 10
80 FOR I=1 TO X STEP 1
90 GOSUB 130
100 PRINT "FIB(";I;")=";A
110 NEXT I
120 STOP
130 REM FIBONACCI RECURSIVE SUBROUTINE
140 IF I>2 THEN 190
150 LET A=1
160 IF D<>1 THEN 180
170 PRINT "FIB(";I;")=";A
180 RETURN
190 LET T=T+1
200 IF T>50 THEN 330
210 LET S(T)=I
220 LET I=I-1
230 GOSUB 140
240 LET K(T)=A
250 LET I=S(T)
260 LET I=I-2
270 GOSUB 140
280 LET K(T)=K(T)+A
290 LET A=K(T)
300 LET I=S(T)
310 LET T=T-1
320 GOTO 160
330 PRINT "STACK OVERFLOW!"
340 STOP
350 END
