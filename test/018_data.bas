10 REM DEMONSTRATE READ, DATA, AND RESTORE
20 REM
30 REM IF YOU DO NOT KNOW HOW MANY ITEMS TO READ, YOU NEED TO HAVE
40 REM A SENTINEL VALUE AT THE END OF THE LIST WITH A VALUE THAT
50 REM ABSOLUTELY CANNOT OCCUR IN THE SET OF NORMAL VALUES YOU READ.
60 REM IN THIS CASE, -999 IS THE SENTINEL VALUE.
70 REM
80 LET C=0
90 READ A
100 IF A=(-999) THEN 170
110 IF A>=0 THEN 140
120 PRINT "YOU READ ";A
130 GOTO 150
140 PRINT "YOU READ";A
150 LET C=C+1
160 GOTO 90
170 PRINT "YOU READ";C;"ITEMS"
180 PRINT
190 REM
200 REM SOMETIMES THE LENGTH OF THE LIST IS IN THE DATA ITSELF AS THE
210 REM FIRST ITEM OF THE LIST.
220 REM
230 LET I=0
240 READ C
250 PRINT "YOU SHOULD READ";C;"ITEMS"
260 IF C=0 THEN 350
270 READ A
280 LET C=C-1
290 LET I=I+1
300 IF A>=0 THEN 330
310 PRINT "ITEM #";I;": ";A
320 GOTO 340
330 PRINT "ITEM #";I;":";A
340 GOTO 260
350 PRINT
360 RESTORE
370 REM
380 REM THE RESTORE STATEMENT RESETS THE READ STATEMENTâ€™S LOCATION
390 REM FOR THE NEXT ITEM TO READ BACK TO THE BEGINNING OF THE
400 REM LIST OF ITEMS.
410 REM
420 REM IF YOU KNOW HOW MANY ITEMS TO READ, YOU CAN USE A FOR LOOP.
430 REM IN THIS CASE, WE KNOW THERE ARE C DATA VALUES TO READ.
440 REM
450 LET C=4
460 PRINT "YOU SHOULD READ";C;"ITEMS"
470 FOR I=1 TO C STEP 1
480 READ A
490 IF A>=0 THEN 520
500 PRINT "ITEM #";I;": ";A
510 GOTO 530
520 PRINT "ITEM #";I;":";A
530 NEXT I
540 STOP
550 DATA 1,2,3,-4,-999
560 DATA 4,1,2,3,-4
570 END
