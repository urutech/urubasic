10 REM      SEQUENTIAL SEARCH DEMO
20 REM
30 REM      A IS ARRAY TO HOLD THE DATA ITEMS
40 REM      I AND J ARE LOOP INDEX VARIABLES
50 REM      X HOLDS THE VALUE WE ARE SEARCHING FOR
60 REM      F IS A FLAG, 0 MEANS NOT FOUND, 1 MEANS FOUND
70 REM      K IS INDEX OF FOUND ELEMENT IN A
80 REM      T IS TOTAL COUNT OF NOT FOUND ENTRIES
90 REM
100 DIM A(19)
110 REM      READ DATA INTO ARRAY A
120 FOR I=0 TO 19
130 READ A(I)
140 NEXT I
150 REM
160 REM      POSITIVE TESTING:
170 REM      NOW ENSURE WE CAN FIND EVERY ELEMENT
180 REM
190 RESTORE
200 LET T=0
210 FOR I=0 TO 19
220 READ X
230 GOSUB 580
240 IF F<>1 THEN 270
250 PRINT X;"FOUND IN SLOT";K
260 GOTO 290
270 PRINT X;"NOT FOUND IN ANY SLOT"
280 LET T=T+1
290 NEXT I
300 IF T<>0 THEN 330
310 PRINT "ALL POSITIVE TESTS SUCCESSFUL!"
320 GOTO 360
330 PRINT T;"POSITIVE TESTS FAILED!"
340 REM
350 REM     NEGATIVE TESTING:
360 REM     NOW ENSURE WE CANNOT FIND ANY ELEMENT NOT IN THE LIST
370 REM
380 LET T=0
390 FOR I=0 TO 79
400 READ X
410 GOSUB 580
420 IF F<>1 THEN 450
430 PRINT X;"FOUND IN SLOT";K
440 GOTO 470
450 PRINT X;"NOT FOUND IN ANY SLOT"
460 LET T=T+1
470 NEXT I
480 IF T<>80 THEN 510
490 PRINT "ALL NEGATIVE TESTS SUCCESSFUL!"
500 GOTO 520
510 PRINT 80-T;"NEGATIVE TESTS FAILED!"
520 STOP
530 REM
540 REM  SUBROUTINE TO SEARCH FOR SCALAR VALUE 'X' IN NUMERIC ARRAY 'A'
550 REM  USING SEQUENTIAL SEARCH ALGORITHM.
560 REM  ON RETURN F=0 MEANS NOT FOUND, F=1 MEANS FOUND IN SLOT K
570 REM
580 FOR J=0 TO 19
590 LET F=0
600 LET K=-1
610 IF A(J)<>X THEN 650
620 LET K=J
630 LET F=1
640 RETURN
650 NEXT J
660 RETURN
670 REM THIS ROW OF DATA IS THE ARRAY DATA WE LOAD
680 DATA 21,85,80,14,60,76,87,49,78,81,96,25,17,22,13,91,23,62,5,57
690 REM THE REST OF THE DATA IS FOR NEGATIVE TESTING AND THESE ELEMENTS
700 REM MUST NOT BE IN THE ARRAY DATA WE LOAD.
710 DATA 1,2,3,4,6,7,8,9,10,11,12,15,16,18,19,20,24,26,27,28,29,30
720 DATA 31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,50,51
730 DATA 52,53,54,55,56,58,59,61,63,64,65,66,67,68,69,70,71,72,73,74
740 DATA 75,77,79,82,83,84,86,88,89,90,92,93,94,95,97,98,99,100
750 END

